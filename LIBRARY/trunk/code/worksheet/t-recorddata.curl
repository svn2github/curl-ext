||| Copyright 1998-2010, Sumisho Computer Systems Corp.  All Rights Reserved.
|||
||| An OpenCurl project.
|||
||| Licensed under the Apache License, Version 2.0 (the "License");
||| you may not use this file except in compliance with the License.
||| You may obtain a copy of the License at
||| 
|||     http://www.apache.org/licenses/LICENSE-2.0
||| 
||| Unless required by applicable law or agreed to in writing, software
||| distributed under the License is distributed on an "AS IS" BASIS,
||| WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
||| See the License for the specific language governing permissions and
||| limitations under the License.
||| ----------------------------------------------------------------------------

{curl 7.0 applet}

{applet manifest = "manifest.mcurl", locale = "en"}

{import * from CURL.UTIL.OBSERVER}
||--{import * from COM.CURL.EXT.GUI}
||--{import * from COM.CURL.EXT.DATA-ACCESS}
||--{import * from CURL.IDE.DOCUMENTATION}

{import * from COM.CURL.EXT.WORKSHEET}

{set-document-properties
    font-size = 10pt
}

|| ----------------------------------------------------------------------------

{title RecordData Worksheet}

{import * from COM.CURL.EXT.WORKSHEET}

|| Create a Worksheet 12 rows by 8 columns
{def worksheet =
    {Worksheet
        12, 7,
        font-size = 9pt,
        row = 1, col = 1, "Sample Data",
        row = 2, col = 2, "multiplier", 0.2, "Affects column C below",
        row = 10, col = 2, "a", "b", "c", "sum",
        row = 11, col = 1, "Totals"
    }
}

|| proc to sum values provided (as range or some other aggregate)
{def compute-sum =
    {proc {dest:DataRef, src:DataRef}:any
        let sum:double = 0.0
        {for v in src do
            {inc sum, v}
        }
        {return sum}
    }
}

|| Sample RecordSet data
{def rs =
    {RecordSet
        {RecordFields
            {RecordField "id",
                domain = int,
                modifiable? = false,
                index-type = RecordFieldIndexType.unique
            },
            {RecordField "a", domain = int},
            {RecordField "b", domain = WorksheetModel.double-domain},
            {RecordField "c", domain = WorksheetModel.double-domain},
            {RecordField "sum", domain = WorksheetModel.double-domain}
        }
    }
}

|| Populate the RecordSet with sample data
{for i = 1 to 6 do
    {rs.append
        {RecordData
            id = i - 1, a = i, b = i * 5, c = (i asa double) / 5, sum = 0
        }
    }
}

|| Turn the RecordSet into a DataSource
{def rds = {RecordSetDataSource rs}}

{value

    || proc to multiply two arguments
    {def compute-product =
        {proc {dest:DataRef, p:DataRef, q:DataRef}:any
            {return {p.get-double} * {q.get-double}}
        }
    }

    || Recompute column c of the RecordSet from $C$2 in the worksheet
    {rds.make-formula {rds.get-ref "c"}, WorksheetModel.double-domain, compute-product,
        {worksheet.model.get-ref "$C$2"}, {rds.get-ref "a"}
    }

    || Add a formula to sum columns a, b, and c in the RecordSet
    {def abc-ref = {rds.get-ref "a/b/c"}}
    {def sum-ref = {rds.get-ref "sum"}}
    {rds.make-formula sum-ref, WorksheetModel.double-domain, compute-sum, abc-ref}

||FIXME: for this to work, need some kind of method to resolve a target
|| so that the recalc methods can all be moved to CalcMixin
||--    {worksheet.model.make-formula sum-ref, compute-sum, abc-ref}

    || Sum each column into row 11 of the worksheet
    {def a-ref = {rds.get-ref "a"}}
    {def b-ref = {rds.get-ref "b"}}
    {def c-ref = {rds.get-ref "c"}}
    {worksheet.model.make-formula-at 11, 2, compute-sum, WorksheetModel.double-domain, a-ref}
    {worksheet.model.make-formula-at 11, 3, compute-sum, WorksheetModel.double-domain, b-ref}
    {worksheet.model.make-formula-at 11, 4, compute-sum, WorksheetModel.double-domain, c-ref}
    {worksheet.model.make-formula-at 11, 5, compute-sum, WorksheetModel.double-domain, sum-ref}

    worksheet || display the worksheet
}

||--{after 2s do
||--    {rds.update}
||--}

{after 3s do
    {worksheet.model.update}
}

[This RecordGrid should be embedded in the worksheet above!]{br}
{RecordGrid
    width = 7in,
    height = 2.0in,
    record-source = rs,
    edit-on-focus? = false,
    region-selection-enabled? = true,
    halign = 1
}
