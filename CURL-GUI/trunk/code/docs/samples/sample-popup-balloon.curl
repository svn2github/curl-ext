||| Maintained by: mgordon

{curl 7.0 applet}
{curl-file-attributes character-encoding = "windows-latin-1"}
{applet 
    manifest = "../manifest.mcurl",
    {compiler-directives careful? = true}
}
{set-document-properties
    font-size = 10pt
}

{import * from COM.CURL.EXT.GUI}

{title Sample popup-balloon}

Click in the calendar to create a popup balloon. Use the
buttons below to excercise the API.

{let view:#View = null}

{let cc:CalendarControl =
    {CalendarControl
        width = 6in,
        height = 4.5in,
        vorigin = "top", horigin = "left",
        value = {DateTime},
        show-date-controls? = false
    }
}

{cc.add-event-handler
    {on e:PointerRelease do
        let content:Dialog = {Dialog background = "white"}
        {content.add
            {spaced-vbox
                {spaced-hbox "Date :", cc.value.info.iso-date},
                {bold Add notes},
                {TextArea
                    width = 1in,
                    height = 1in
                }
            }
        }
        {set view =
            {popup-balloon
                content,
                cc,
                title = "Daily Notes:",
                x = e.x,
                y = e.y,
                tail-size = 9pt,
                max-width = 5in,
                corner-radius = 9pt
            }
        }
    }
}

{value cc}


{CommandButton
    label = "Add Content",
    {on Action do
        {if-non-null view then
            def c = {TextArea width = 2in, height = 1in}
            set view.title = "Added contents"
            {view.add c, replace? = true}
            set view.title = "Added New Content"
        }
    }
}
{CommandButton
    label = "Clear Popup",
    {on Action do
        {if-non-null view then
            {view.clear}
            set view.title = "Cleared contents"
        }
    }
}
{CommandButton
    label = "Hide for 2 sec.",
    {on Action do
        {if-non-null view then
            {view.hide}
            {after 2s do
                {view.show}
            }
        }
    }
}
{CommandButton
    label = "Move",
    {on Action do
        {if-non-null view then
            {(view asa #BalloonView).set-pointer-position 0in, 0in}
||--                0.0681465m, 0.0269503m}
        }
    }
}

{bold Known Issues}
{itemize
    {item Sometimes when you clear the content the window resizes to
        the wrong size and the close button is partly hidden. This
        only seems to happen with the initial content.
    }
    {item If the applet is in a tabbed browser, activating another tab
        does not hide the popups.
    }
    {item When you scroll the browser window such that the tip of 
        the pointer is obscured, the popup disappears. But if you move
        the browser to the bottom edge of the screen, the popup
        remains. Perhaps it should disappear when the tip of the
        pointer goes off screen.
    }
    {item Review the API}
    {item Write complete doc strings}
    {item Test and Fix}
}

