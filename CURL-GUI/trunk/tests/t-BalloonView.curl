||| Maintained by: mgordon

{curl 7.0 applet}
{applet manifest = "manifest.mcurl",
    {compiler-directives careful? = true}
}

{import * from COM.CURL.EXT.AUTO-TEST}
{import * from COM.CURL.EXT.GUI}

{import * from CURL.GRAPHICS.WINDOW}

{import * from CURL.GUI.STANDARD} || For events
{import * from CURL.GUI.CONTROL-BASE} || Control

|| Run this script using curl.exe:
|| "C:\Program Files\Curl Corporation\Surge\8\bin\curl.exe" %1 --manual

{let package balloon:#BalloonView = null}

{define-proc {make-test ...}:Graphic
    def args = {Arguments ...}
    let cc:CalendarControl =
        {CalendarControl
            vorigin = "top", horigin = "left",
            value = {DateTime},
            show-date-controls? = false,
            {on e:PointerRelease do
                let content:Dialog = {Dialog background = "white"}
                {content.add
                    {spaced-vbox
                        {spaced-hbox "Date:", cc.value.info.iso-date},
                        {bold Add notes},
                        {TextArea
                            width = 1in,
                            height = 1in
                        }
                    }
                }
                {if-non-null bv = balloon then
                    {bv.close-popup}
                }
                set balloon =
                    {BalloonView
                        content,
                        cc,
                        e.x, e.y,
||--                        x = e.x,
||--                        y = e.y,
                        title = "Daily Notes",
                        tail-size = 9pt,
                        max-width = 5in,
                        corner-radius = 9pt,
                        {on e:PointerMotion at bv:BalloonView do
                            {dump e.x, e.y}
                        },
                        {splice args}
                    }
            }
        }
    {return cc}
}

|| -----------------
|| Define the tests
|| -----------------

{def value-changed = {ValueChanged}}
{def value-finished = {ValueFinished}}
{def action = {Action}}
{def key-press = {KeyPress}}

{def test-close =
    {GuiTest {make-test close-button? = true}, "Test Close Button",
        {UI.focus},
        {UI.click 10mm, 5mm, delay = 100ms},
        {UI.set-view {proc {}:#View {return balloon}}},
        {UI.click 8mm, 28mm, delay = 100ms},
        {UI.type "tested", view = balloon},
        {UI.click 27mm, 10mm, delay = 100ms},
        {UI.wait 2s},
        {UI.check
            check-proc =
                {proc {t:Graphic, args:Array
                      }:(expected:Array, actual:Array)
                    def expected = {Array true}
                    def actual = {Array balloon.destroyed?}
                    {return (expected, actual)}
                }
        }
    }
}

{let dialog:Dialog =
    {Dialog
        {big {bold Test Dialog}}
    }
}


{def test-no-close =
    {GuiTest {make-test close-button? = false}, "Test no close button",
        {UI.focus},
        {UI.click 10mm, 5mm, delay = 100ms},
        {UI.set-view {proc {}:#View {return balloon}}},
        {UI.click 8mm, 28mm, delay = 100ms},
        {UI.type "tested", view = balloon},
        {UI.click 27mm, 10mm, delay = 100ms},
        {UI.check
            check-proc = {proc {t:Graphic, args:Array
                               }:(expected:Array, actual:Array)
                             def expected = {Array false}
                             def actual = {Array balloon.destroyed?}
                             {return (expected, actual)}
                         }
        }
        ,
        {UI.do
            {proc {t:GuiTest}:void
                set dialog = {Dialog {big {bold Test Dialog!}}}
                {dialog.show modal? = false}
            },
            delay = .1s
        }
,        {UI.check
            check-proc = {proc {t:Graphic, args:Array
                               }:(expected:Array, actual:Array)
                             def expected = {Array true}
                             def actual = {Array balloon.destroyed?}
                             {return (expected, actual)}
                         }
        },
        {UI.do
            {proc {t:GuiTest}:void
                {dialog.close "closed"}
            }
        }
    }
}


|| Initialize the GuiTest framework and get a table for the test results
{GuiTest.setup "BalloonView Tests"
||--        ,run-all-proc =
||--            {proc {}:void
||--                {test-no-close.run}
||--            }
}
